*** Set the path;
%LET PATH = C:/Users/sdutta3/OneDrive - University of Kansas Medical Center/ENAR DataFest/Data;

*** Import the cleaned data sets;
PROC IMPORT DATAFILE="&PATH/dat_2013_2014_new.csv" 
	OUT=data_2013_2014 REPLACE
	DBMS=CSV;
RUN;

PROC IMPORT DATAFILE="&PATH/dat_2017_2020_new.csv" 
	OUT=data_2017_2020 REPLACE
	DBMS=CSV;
RUN;

** Creating data set for Propensity score matching;
DATA DAT_PREPROPENSITY;
	SET WORK.DATA_2013_2014
		WORK.DATA_2017_2020;
RUN;

PROC SORT DATA=WORK.DAT_PREPROPENSITY;
	BY svy_strata;
RUN;
PROC SUMMARY DATA=WORK.DAT_PREPROPENSITY SUM;
	VAR bp_sys_mean;
	BY svy_strata;
	OUTPUT OUT=WORK.STRATUMTOTAL SUM=_TOTAL_;
RUN;

** Divide data into high and low sodium;
DATA WORK.LOWSODIUM_PREPON WORK.HIGHSODIUM_PREPON;
	SET WORK.DAT_PREPROPENSITY;
	IF SODIUM >= 2300 THEN DO;
		Sodium_cat = "High";
		OUTPUT WORK.HIGHSODIUM_PREPON;
		END;
	ELSE DO;
		Sodium_cat = "Low";
		OUTPUT WORK.LOWSODIUM_PREPON;
	END;
RUN;

PROC FREQ DATA=WORK.LOWSODIUM_PREPON;
	TABLES SODIUM_CAT*svy_year/ NOCOL NOROW NOCUM NOPERCENT;
RUN;

PROC FREQ DATA=WORK.HIGHSODIUM_PREPON;
	TABLES SODIUM_CAT*svy_year/ NOCOL NOROW NOCUM NOPERCENT;
RUN;

ODS GRAPHICS ON;
PROC PSMATCH DATA=WORK.HIGHSODIUM_PREPON REGION=treated(extend(stat=ps mult=one)=0.025) ;
	CLASS demo_race demo_gender svy_year demo_age_years;
	PSMODEL svy_year (tREATED='2013-2014') = demo_race demo_gender demo_age_years;
	match stat=ps method=varratio(kmin=1 kmax=3) exact=(demo_gender) caliper=.;
	assess ps var=(demo_race demo_gender )
		/ plots(orient=vertical);
output out(obs=match)=Ps_match_HighSodium matchid=_MatchID;
run;

ODS GRAPHICS ON;
PROC PSMATCH DATA=WORK.LOWSODIUM_PREPON REGION=treated(extend(stat=ps mult=one)=0.025);
	CLASS demo_race demo_gender svy_year demo_age_years;
	PSMODEL svy_year (tREATED='2013-2014') = demo_race demo_gender demo_age_years;
	match stat=ps method=varratio(kmin=1 kmax=3) exact=(demo_gender) caliper=.;
	assess ps var=(demo_race demo_gender )
		/ plots(orient=vertical);
output out(obs=match)=Ps_match_LowSodium matchid=_MatchID;
run;

PROC SUMMARY DATA=WORK.PS_MATCH_HIGHSODIUM MAX PRINT;
	VAR _MatchID;
RUN;

PROC SUMMARY DATA=WORK.PS_MATCH_LOWSODIUM MAX PRINT;
	VAR _MatchID;
RUN;

** Combine the two sodium groups together;
DATA DID_PREDATA;
	SET WORK.PS_MATCH_HIGHSODIUM WORK.PS_MATCH_LOWSODIUM;
	ATT_WT = _PS_/(1-_PS_);
	IF svy_year = "2013-2014" THEN POST = 0;
		ELSE POST=1;
	IF SODIUM_CAT = "High" THEN EXPOSED = 1;
		ELSE EXPOSED=0;
	IF BP_SYS_MEAN <130 AND bp_dia_mean <80 THEN CONTROLLED=1;
		ELSE CONTROLLED= 0;
RUN;

*** Adjust using all other variables;
PROC GENMOD DATA=WORK.DID_PREDATA;
	CLASS htn_aware /*bp_med_use*/ cc_smoke cc_bmi
		  cc_diabetes cc_ckd cc_cvd_any edu ;
	MODEL bp_sys_mean = htn_aware /*bp_med_use*/ cc_smoke cc_bmi
								cc_diabetes cc_ckd cc_cvd_any edu alcohol;
	OUTPUT OUT=WORK.DID_PREDATA1 PRED=DID_RESPONSE;
RUN;	

** DID coding;
PROC FORMAT;
	VALUE EXPOSED_MIXED (multilabel)
						1 = "High Sodium Intake"
						0 = "Low Sodium Intake";
	VALUE PREPOST_MIXED (multilabel)
			   0 = "2013-2014"
			   1 = "2017-2020";
	VALUE HTN_MIXED (multilabel)
			   1 = "CONTROL BP"
			   0 = "NOT CONTROL BP";
RUN;

*ODS TRACE ON;
ODS OUTPUT LSMEANS=DID_LS;
PROC GENMOD DATA = DID_PREDATA1 order=formatted;
	CLASS POST EXPOSED/ REF=FIRST;
	MODEL  DID_RESPONSE=POST|EXPOSED; 
	LSMEANS POST|EXPOSED / DIFF;
	*RANDOM exposed;
	ESTIMATE 'D-I-D' EXPOSED*POST 1 -1 -1 1;
	FORMAT EXPOSED EXPOSED_MIXED. POST PREPOST_MIXED.;
	TITLE3 "UNADJUSTED: INCLUDING LEAST-SQUARES MEANS ESTIMATES";
RUN;
TITLE;

PROC SGPLOT DATA=WORK.DID_LS;
	WHERE POST NOT IS MISSING AND EXPOSED NOT IS MISSING;
	SCATTER X=POST Y=Estimate/ GROUP=EXPOSED;
	SERIES X=POST Y=Estimate/ GROUP=EXPOSED;
RUN;

ODS OUTPUT LSMEANS=DID_LS_ADJUSTED;
PROC genmod DATA = DID_PREDATA1 order=formatted;
	where bp_med_use = "Yes" or controlled = 0;
	CLASS POST EXPOSED/ REF=FIRST;
	MODEL  DID_RESPONSE=POST|EXPOSED;
	LSMEANS POST|EXPOSED / DIFF;
	WEIGHT ATT_WT;
	*RANDOM controlled;
	ESTIMATE 'D-I-D' EXPOSED*POST 1 -1 -1 1;
	FORMAT EXPOSED EXPOSED_MIXED. POST PREPOST_MIXED.;
	TITLE3 "UNADJUSTED: INCLUDING LEAST-SQUARES MEANS ESTIMATES";
	output out=ADJUSTED_PRED PRED=PRED;
RUN;
TITLE;

PROC SGPLOT DATA=WORK.DID_LS_ADJUSTED;
	WHERE POST NOT IS MISSING AND EXPOSED NOT IS MISSING;
	SCATTER X=POST Y=Estimate/ GROUP=EXPOSED;
	SERIES X=POST Y=Estimate/ GROUP=EXPOSED;
RUN;

PROC SORT DATA=WORK.ADJUSTED_PRED;
	BY POST EXPOSED bp_med_use;
RUN;

PROC SUMMARY DATA=WORK.ADJUSTED_PRED MEAN;
	BY POST EXPOSED bp_med_use;
	VAR PRED;
	OUTPUT OUT=PLOTDATA MEAN=MEAN;
RUN;

DATA GROUP;
	SET WORK.PLOTDATA;
	IF EXPOSED = 0 AND bp_med_use = "No" then group=1;
	ELSE IF EXPOSED = 0 AND bp_med_use = "Yes" then group=2;
	ELSE IF EXPOSED = 1 AND bp_med_use = "No" then group=3;
	ELSE group=4;
RUN;

PROC SGPLOT DATA=WORK.GROUP;
	*WHERE POST NOT IS MISSING AND EXPOSED NOT IS MISSING;
	SCATTER X=POST Y=MEAN/ GROUP=GROUP;
	SERIES X=POST Y=MEAN/ GROUP=GROUP;
RUN;

** CONTROL BP;
ODS OUTPUT LSMEANS=DID_LS1;
PROC GENMOD DATA = DID_PREDATA1 order=formatted;
	CLASS POST EXPOSED/ REF=FIRST;
	MODEL  CONTROLLED (REF='0')=POST|EXPOSED/ DIST=BINOMIAL; 
	LSMEANS POST|EXPOSED / DIFF;
	*RANDOM exposed;
	ESTIMATE 'D-I-D' EXPOSED*POST 1 -1 -1 1;
	FORMAT EXPOSED EXPOSED_MIXED. POST PREPOST_MIXED.;
	TITLE3 "UNADJUSTED: INCLUDING LEAST-SQUARES MEANS ESTIMATES";
RUN;
TITLE;

PROC SGPLOT DATA=WORK.DID_LS1;
	WHERE POST NOT IS MISSING AND EXPOSED NOT IS MISSING;
	SCATTER X=POST Y=Estimate/ GROUP=EXPOSED;
	SERIES X=POST Y=Estimate/ GROUP=EXPOSED;
RUN;

ODS OUTPUT LSMEANS=DID_LS_ADJUSTED1;
PROC GENMOD DATA = DID_PREDATA1 order=formatted;
	CLASS POST EXPOSED/ REF=FIRST;
	MODEL  CONTROLLED (REF='0')=POST|EXPOSED/ DIST=BINOMIAL; 
	LSMEANS POST|EXPOSED / DIFF;
	WEIGHT ATT_WT;
	ESTIMATE 'D-I-D' EXPOSED*POST 1 -1 -1 1;
	FORMAT EXPOSED EXPOSED_MIXED. POST PREPOST_MIXED.;
	TITLE3 "ADJUSTED: INCLUDING LEAST-SQUARES MEANS ESTIMATES";
RUN;
TITLE;

PROC SGPLOT DATA=WORK.DID_LS_ADJUSTED1;
	WHERE POST NOT IS MISSING AND EXPOSED NOT IS MISSING;
	SCATTER X=POST Y=Estimate/ GROUP=EXPOSED;
	SERIES X=POST Y=Estimate/ GROUP=EXPOSED;
RUN;
